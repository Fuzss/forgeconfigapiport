apply plugin: "com.github.johnrengelman.shadow"

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":Common").loom.accessWidenerPath

    runs {
        configureEach {
            ideConfigGenerated(true)
            runDir("../run")
            vmArgs '-Xms1G', '-Xmx4G', '-Dmixin.debug.export=true'
        }
        client {
            client()
            setConfigName("Fabric Client ${libs.versions.minecraft.get()}")
        }
        server {
            server()
            setConfigName("Fabric Server ${libs.versions.minecraft.get()}")
        }
    }
}

configurations {
    common
    shadowCommon
    commonNeoForgeApi
    shadowCommonNeoForgeApi
    compileClasspath.extendsFrom common, commonNeoForgeApi
    runtimeClasspath.extendsFrom common, commonNeoForgeApi
    developmentFabric.extendsFrom common, commonNeoForgeApi
}

repositories {
    maven {
        name = "Modmuss"
        url = "https://maven.modmuss50.me/"
    }
    maven {
        name = "ladysnake"
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
    }
    maven {
        name = "jamieswhiteshirt"
        url = "https://maven.jamieswhiteshirt.com/libs-release/"
    }
}

dependencies {
    // Common Project
    common(project(path: ":Common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":Common", configuration: "transformProductionFabric")) { transitive false }
    commonNeoForgeApi(project(path: ":Common-NeoForgeApi", configuration: "namedElements")) { transitive false }
    shadowCommonNeoForgeApi(project(path: ":Common-NeoForgeApi", configuration: "transformProductionFabric")) { transitive false }

    // Fabric Loader
    modApi libs.fabricloader.fabric

    // Fabric Api
    modApi libs.fabricapi.fabric

    // Night Config
    api(include(libs.nightconfigcore.get()))
    api(include(libs.nightconfigtoml.get()))

    // Puzzles Lib
//    modApi libs.puzzleslib.fabric

    // Cardinal Components
//    modApi(include(libs.cardinalcomponentsbase.fabric.get()))
//    modApi(include(libs.cardinalcomponentsentity.fabric.get()))
//    modApi(include(libs.cardinalcomponentsblock.fabric.get()))
//    modApi(include(libs.cardinalcomponentschunk.fabric.get()))
//    modApi(include(libs.cardinalcomponentsworld.fabric.get()))

    // Extensible Enums
//    modApi(include(libs.extensibleenums.fabric.get()))

    var versionCatalog = rootProject.extensions.getByType(VersionCatalogsExtension).named("libs")

    // Quality of Life Mods
    versionCatalog.findLibrary("modmenu.fabric").ifPresent {
        modLocalRuntime it
    }
    versionCatalog.findLibrary("forgeconfigscreens.fabric").ifPresent {
        modLocalRuntime it
    }
}

shadowJar {
    configurations = [project.configurations.shadowCommon, project.configurations.shadowCommonNeoForgeApi]
    archiveClassifier.set("dev-shadow")
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set("")
    injectAccessWidener = true
}

jar {
    archiveClassifier.set("dev")
}

sourcesJar {
    def commonSources = project(":Common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
    def commonNeoForgeApiSources = project(":Common-NeoForgeApi").sourcesJar
    dependsOn commonNeoForgeApiSources
    from commonNeoForgeApiSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}
