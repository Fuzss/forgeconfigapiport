architectury {
    common rootProject.subprojects.name.stream().filter { it.toLowerCase() != "common" }.map { it.toLowerCase() }.toArray() as String[]
}

loom {
    accessWidenerPath = file("src/main/resources/${modId}.accesswidener")

    runs {
        configureEach {
            ideConfigGenerated(false)
            // this should hopeful prevent an empty run directory being generated in common during initial project setup
            runDir("../run")
            vmArgs '-Xms1G', '-Xmx4G', '-Dmixin.debug.export=true', '-Dmixin.debug.verbose=true'
        }
        client {
            client()
            setConfigName("Common Client ${libs.versions.minecraft.get()}")
        }
        server {
            server()
            setConfigName("Common Server ${libs.versions.minecraft.get()}")
        }
    }
}

dependencies {
    // Fabric Loader
    modApi libs.fabricloader.fabric

    // Mixin Extras
    api libs.mixinextras
    annotationProcessor libs.mixinextras

    // Night Config
    api libs.nightconfigcore
    api libs.nightconfigtoml
}

jar {
    manifest {
        attributes 'Fabric-Loom-Remap': true
    }
}
